/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import analysis.asymptotics.asymptotics
import analysis.asymptotics.superpolynomial_decay
import analysis.special_functions.polynomials

/-!
# Polynomial Growth

This file defines polynomial growth of functions as `asymptotics.polynomial_growth l k f`.
A function `f : Œ± ‚Üí E` has polynomial growth in the parameter `k : Œ± ‚Üí S` on the filter `l` if
  there exists `n : ‚Ñï` such that `f(x)` is `O(k(x) ^ n)`.
Note `f` and `k` may have different domains, and only the domain of `k` needs a ring structure.
Most theorems assume little about the domain of `f`, so the file is organized by the domain of `k`.

Equivalently `f(x)` is polynomial growth in `k` if `f` is `O(p(k(x)))` for some polynomial `p`
  (see `polynomial_growth_iff_is_O_polynomial`).

When the parameter is a linear inclusion, this gives standard polynomial growth.
When the parameter is logarithmic, it gives polylogarithmic growth as described here:
https://en.wikipedia.org/wiki/Polylogarithmic_function

# Main Theorems

* `polynomial_growth.polynomial_eval`: A polynomial evaluated at a polynomial growth function
    is also polynomial growth, assuming `‚à•k x‚à•` is eventually bounded below by `1`.
* `polynomial_growth_of_norm_bdd_above`: A function with bounded norm must have polynomial growth
-/

namespace asymptotics

open filter polynomial

/-- A function `f` has polynomial growth in parameter `k` if `f` is `O(k(x)^n)` for some `n : ‚Ñï`.
  The domain of `f` can be any normed space, but the domain of `k` must be a normed ring. -/
def polynomial_growth {Œ± E K : Type*} [has_norm E] [normed_ring K]
  (l : filter Œ±) (k : Œ± ‚Üí K) (f : Œ± ‚Üí E) :=
‚àÉ (n : ‚Ñï), is_O f (Œª x, (k x) ^ n) l

variables {Œ± : Type*} {l : filter Œ±}
variables {E E' S S' R R' : Type*} [has_norm E] [has_norm E']
   [normed_group S] [normed_group S'] [normed_ring R] [normed_ring R']

section normed_ring

variables {K : Type*} [normed_ring K] {k : Œ± ‚Üí K}

lemma polynomial_growth.mono {f : Œ± ‚Üí S} {g : Œ± ‚Üí E}
  (hf : polynomial_growth l k f) (hfg : ‚àÄ x, ‚à•g x‚à• ‚â§ ‚à•f x‚à•) :
  polynomial_growth l k g :=
let ‚ü®n, hn‚ü© := hf in ‚ü®n, (is_O_of_le l hfg).trans hn‚ü©

lemma polynomial_growth.is_O_trans {f : Œ± ‚Üí S} {g : Œ± ‚Üí E}
  (hf : polynomial_growth l k f) (h : is_O g f l) :
  polynomial_growth l k g :=
let ‚ü®n, hn‚ü© := hf in ‚ü®n, h.trans hn‚ü©

lemma polynomial_growth.eventually_trans {f : Œ± ‚Üí S} {g : Œ± ‚Üí E}
  (hf : polynomial_growth l k f) (h : ‚àÄ·∂† x in l, ‚à•g x‚à• ‚â§ ‚à•f x‚à•) :
  polynomial_growth l k g :=
hf.is_O_trans $ is_O_iff.2 ‚ü®1, by simpa using h‚ü©

variables (l k)

/-- For any parameter `k`, it is polynomial growth in itself. -/
@[simp]
lemma polynomial_growth_parameter :
  polynomial_growth l k k :=
‚ü®1, by simpa only [pow_one] using is_O_refl k l‚ü©

@[simp]
lemma polynomial_growth_const [norm_one_class K] (x : E) :
  polynomial_growth l k (Œª _, x) :=
‚ü®0, is_O_of_le' l (Œª x, by simp only [mul_one, norm_one, pow_zero])‚ü©

lemma polynomial_growth_zero [norm_one_class K] [has_zero S] :
  polynomial_growth l k (0 : Œ± ‚Üí S) :=
polynomial_growth_const l k 0

lemma polynomial_growth_one [norm_one_class K] [has_one S] :
  polynomial_growth l k (1 : Œ± ‚Üí S) :=
polynomial_growth_const l k 1

variables {l k}

end normed_ring

lemma polynomial_growth_of_superpolynomial_decay {Œ± ùïú : Type*} [ordered_comm_semiring Œ±]
  [normed_field ùïú] [algebra Œ± ùïú] (f : Œ± ‚Üí ùïú) (hf : superpolynomial_decay f) :
  polynomial_growth at_top (algebra_map Œ± ùïú) f :=
‚ü®0, by simpa only [gpow_zero, pow_zero] using hf 0‚ü©

section normed_field

variables {K : Type*} [normed_field K] {k : Œ± ‚Üí K}

/-- Polynomial growth in `k` is additive if `k` eventually has norm at least `1` -/
lemma polynomial_growth.add {f g : Œ± ‚Üí S}
  (hf : polynomial_growth l k f) (hg : polynomial_growth l k g)
  (hk : ‚àÄ·∂† x in l, 1 ‚â§ ‚à•k x‚à•) : polynomial_growth l k (f + g) :=
let ‚ü®n, hn‚ü© := hf in let ‚ü®m, hm‚ü© := hg in
‚ü®max n m, is_O.add (hn.trans $ is_O_pow_pow_of_le hk (le_max_left n m))
  (hm.trans $ is_O_pow_pow_of_le hk (le_max_right n m))‚ü©

/-- Polynomial growth is multiplicative for arbitrary parameters -/
lemma polynomial_growth.mul {f g : Œ± ‚Üí R}
  (hf : polynomial_growth l k f) (hg : polynomial_growth l k g) :
  polynomial_growth l k (f * g) :=
let ‚ü®n, hn‚ü© := hf in let ‚ü®m, hm‚ü© := hg in
‚ü®n + m, (is_O.mul hn hm).trans $ is_O_of_le l (Œª x, (pow_add (k x) n m) ‚ñ∏ le_rfl)‚ü©

lemma polynomial_growth.pow {f : Œ± ‚Üí R}
  (hf : polynomial_growth l k f) (n : ‚Ñï) :
  polynomial_growth l k (f ^ n) :=
let ‚ü®m, hm‚ü© := hf in
  ‚ü®m * n, (is_O.pow hm n).trans $ is_O_of_le l (Œª x, (pow_mul (k x) m n) ‚ñ∏ le_rfl)‚ü©

/-- A polynomial evaluated at a polynomial growth function is polynomial growth,
  assuming `k` eventually has norm at least `1` -/
theorem polynomial_growth.polynomial_eval {f : Œ± ‚Üí R}
  (hf : polynomial_growth l k f) (hk : ‚àÄ·∂† x in l, 1 ‚â§ ‚à•k x‚à•)
  (p : polynomial R) : polynomial_growth l k (Œª x, eval (f x) p) :=
begin
  refine p.induction_on (Œª c, _) (Œª p q hp hq, _) (Œª n c h, _),
  { exact (polynomial_growth_const l k c).mono (Œª x, le_of_eq $ congr_arg _ eval_C) },
  { exact (hp.add hq hk).mono (Œª x, le_of_eq $ congr_arg _ eval_add) },
  { exact (h.mul (hf)).mono (Œª x, le_of_eq $ congr_arg _ $
      by simp only [eval_C, eval_mul_X_pow, pi.mul_apply, pow_add (f x) n 1, mul_assoc, pow_one]) }
end

end normed_field

section nondiscrete_normed_field

variables {ùïú : Type*} [nondiscrete_normed_field ùïú] {k : Œ± ‚Üí ùïú}

/-- If the norm of `f` is bounded above then it has polynomial growth,
  assuming `k` maps into a `nondiscrete_normed_field` -/
lemma polynomial_growth_of_norm_bdd_above
  {f : Œ± ‚Üí E} (hf : bdd_above (set.range (Œª x, ‚à•f x‚à•))) :
  polynomial_growth l k f :=
let ‚ü®c, hc‚ü© := hf in
let ‚ü®y, hy‚ü© := normed_field.exists_lt_norm ùïú c in
(polynomial_growth_const l k y).mono $
  Œª x, ((mem_upper_bounds.1 hc) (‚à•f x‚à•) (set.mem_range_self x)).trans (le_of_lt hy)

lemma polynomial_growth_of_norm_eventually_le
  {f : Œ± ‚Üí E} (b : ‚Ñù) (hf : ‚àÄ·∂† x in l, ‚à•f x‚à• ‚â§ b) :
  polynomial_growth l k f :=
let ‚ü®y, hy‚ü© := normed_field.exists_lt_norm ùïú b in
(polynomial_growth_const l k y).eventually_trans $
  sets_of_superset l hf (Œª x hx, (le_trans hx (le_of_lt hy) : ‚à•f x‚à• ‚â§ ‚à•y‚à•))

end nondiscrete_normed_field

section normed_linear_ordered_field

variables {ùïú : Type*} [normed_linear_ordered_field ùïú] [order_topology ùïú] {k : Œ± ‚Üí ùïú}

/-- Equivalence of definition in terms of powers and polynomials, assuming order topology on `ùïú`,
  and that the parameter tendsto to `at_top` -/
theorem polynomial_growth_iff_is_O_polynomial (hk : tendsto k l at_top)
  (f : Œ± ‚Üí E) : polynomial_growth l k f ‚Üî
    ‚àÉ (p : polynomial ùïú), is_O f (Œª x, eval (k x) p) l :=
begin
  refine ‚ü®Œª h, let ‚ü®n, hn‚ü© := h in ‚ü®X ^ n, by simpa‚ü©, _‚ü©,
  rintro ‚ü®p, hp‚ü©,
  refine ‚ü®p.nat_degree, is_O.trans hp _‚ü©,
  have : is_O ((Œª a, eval a p) ‚àò k) ((Œª a, eval a (X ^ p.nat_degree)) ‚àò k) l,
  from is_O.comp_tendsto (polynomial.is_O_of_degree_le p (X ^ p.nat_degree) (by simp)) hk,
  simpa only [eval_X, eval_pow] using this,
end

lemma polynomial_growth_of_is_O_polynomial (hk : tendsto k l at_top)
  (f : Œ± ‚Üí E) (p : polynomial ùïú) (h : is_O f (Œª x, eval (k x) p) l) :
  polynomial_growth l k f :=
(polynomial_growth_iff_is_O_polynomial hk f).2 ‚ü®p, h‚ü©

end normed_linear_ordered_field

end asymptotics
