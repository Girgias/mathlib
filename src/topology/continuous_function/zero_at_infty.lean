/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import topology.continuous_function.bounded

/-!
# Continuous functions vanishing at infinity

The type of continuous functions vanishing at infinity. When the domain is compact
`C(Œ±, Œ≤) ‚âÉ (Œ± ‚ÜíC‚ÇÄ Œ≤)` via the identity map. When the codomain is a metric space, every continuous
map which vanishes at infinity is a bounded continuous function. When the domain is a locally
compact space, this type has nice properties.
-/
universes u v w

variables {F : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} [topological_space Œ±]

open_locale bounded_continuous_function topological_space
open filter metric

section preliminaries
--- these results need to be moved to appropriate places

@[simp]
lemma filter.cocompact_eq_bot [compact_space Œ±] : cocompact Œ± = ‚ä• :=
has_basis_cocompact.eq_bot_iff.mpr ‚ü®set.univ, compact_univ, set.compl_univ‚ü©

end preliminaries

/-- `Œ± ‚ÜíC‚ÇÄ Œ≤)` is the type of continuous functions `Œ± ‚Üí Œ≤` which vanish at infinity from a
topological space to a metric space with a zero element.

When possible, instead of parametrizing results over `(f : Œ± ‚ÜíC‚ÇÄ Œ≤)`,
you should parametrize over `(F : Type*) [zero_at_infty_continuous_map_class F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `zero_at_infty_continuous_map_class`. -/
structure zero_at_infty_continuous_map (Œ± : Type u) (Œ≤ : Type v)
  [topological_space Œ±] [has_zero Œ≤] [topological_space Œ≤] extends continuous_map Œ± Œ≤ :
  Type (max u v) :=
(zero_at_infty' : tendsto to_fun (cocompact Œ±) (ùìù 0))

localized "notation  Œ± ` ‚ÜíC‚ÇÄ ` Œ≤ := zero_at_infty_continuous_map Œ± Œ≤" in zero_at_infty

/-- `zero_at_infty_continuous_map_class F Œ± Œ≤` states that `F` is a type of continuous maps which
vanish at infinity.

You should also extend this typeclass when you extend `zero_at_infty_continuous_map`. -/
class zero_at_infty_continuous_map_class (F Œ± Œ≤ : Type*) [topological_space Œ±] [has_zero Œ≤]
  [topological_space Œ≤] extends continuous_map_class F Œ± Œ≤ :=
(zero_at_infty (f : F) : tendsto f (cocompact Œ±) (ùìù 0))

export zero_at_infty_continuous_map_class (zero_at_infty)

namespace zero_at_infty_continuous_map

section basics

variables [topological_space Œ≤] [has_zero Œ≤] [zero_at_infty_continuous_map_class F Œ± Œ≤]

instance : zero_at_infty_continuous_map_class (Œ± ‚ÜíC‚ÇÄ Œ≤) Œ± Œ≤ :=
{ coe := Œª f, f.to_fun,
  coe_injective' := Œª f g h, by { obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f, obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g, congr' },
  map_continuous := Œª f, f.continuous_to_fun,
  zero_at_infty := Œª f, f.zero_at_infty' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`
directly. -/
instance : has_coe_to_fun (Œ± ‚ÜíC‚ÇÄ Œ≤) (Œª _, Œ± ‚Üí Œ≤) := fun_like.has_coe_to_fun

instance : has_coe_t F (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª f, { to_fun := f, continuous_to_fun := map_continuous f, zero_at_infty' := zero_at_infty f }‚ü©

@[simp] lemma coe_to_continuous_fun (f : Œ± ‚ÜíC‚ÇÄ Œ≤) : (f.to_continuous_map : Œ± ‚Üí Œ≤) = f := rfl


@[ext] lemma ext {f g : Œ± ‚ÜíC‚ÇÄ Œ≤} (h : ‚àÄ x, f x = g x) : f = g := fun_like.ext _ _ h

/-- Copy of a `zero_at_infinity_continuous_map` with a new `to_fun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : Œ± ‚ÜíC‚ÇÄ Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : Œ± ‚ÜíC‚ÇÄ Œ≤ :=
{ to_fun := f',
  continuous_to_fun := by { rw h, exact f.continuous_to_fun },
  zero_at_infty' := by { simp_rw h, exact f.zero_at_infty' } }

lemma eq_of_empty [is_empty Œ±] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤) : f = g :=
ext $ is_empty.elim ‚Äπ_‚Ä∫

/-- A continuous function on a compact space is automatically a continuous function vanishing at
infitnity. -/
@[simps]
def continuous_map.lift_zero_at_infty [compact_space Œ±] : C(Œ±, Œ≤) ‚âÉ (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
{ to_fun := Œª f, { to_fun := f, continuous_to_fun := f.continuous, zero_at_infty' := by simp },
  inv_fun := Œª f, f,
  left_inv := Œª f, by { ext, refl },
  right_inv := Œª f, by { ext, refl } }

/-- A continuous function on a compact space is automatically a continuous function vanishing at
infitnity. This is not an instance to avoid type class loops. -/
def zero_at_infty_continuous_map_class.of_compact {G : Type*} [continuous_map_class G Œ± Œ≤]
  [compact_space Œ±] : zero_at_infty_continuous_map_class G Œ± Œ≤ :=
{ coe := Œª g, g,
  coe_injective' := Œª f g h, fun_like.coe_fn_eq.mp h,
  map_continuous := map_continuous,
  zero_at_infty := by simp }

end basics

section algebraic_structure

variables [topological_space Œ≤] (x : Œ±)

instance [has_zero Œ≤] : has_zero (Œ± ‚ÜíC‚ÇÄ Œ≤) := ‚ü®‚ü®0, tendsto_const_nhds‚ü©‚ü©

instance [has_zero Œ≤] : inhabited (Œ± ‚ÜíC‚ÇÄ Œ≤) := ‚ü®0‚ü©

@[simp] lemma coe_zero [has_zero Œ≤] : ‚áë(0 : Œ± ‚ÜíC‚ÇÄ Œ≤) = 0 := rfl
lemma zero_apply [has_zero Œ≤] : (0 : Œ± ‚ÜíC‚ÇÄ Œ≤) x = 0 := rfl

instance [mul_zero_class Œ≤] [has_continuous_mul Œ≤] : has_mul (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª f g, ‚ü®f * g, by simpa only [mul_zero] using ((zero_at_infty f).mul (zero_at_infty g) :
  tendsto (Œª x : Œ±, f x * g x) (cocompact Œ±) (ùìù (0 * 0)))‚ü©‚ü©

@[simp] lemma coe_mul [mul_zero_class Œ≤] [has_continuous_mul Œ≤] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤) :
  ‚áë(f * g) = f * g := rfl
lemma mul_apply [mul_zero_class Œ≤] [has_continuous_mul Œ≤] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤) :
  (f * g) x = f x * g x := rfl

instance [mul_zero_class Œ≤] [has_continuous_mul Œ≤] : mul_zero_class (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.mul_zero_class _ coe_zero coe_mul

instance [semigroup_with_zero Œ≤] [has_continuous_mul Œ≤] : semigroup_with_zero (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.semigroup_with_zero _ coe_zero coe_mul

instance [add_zero_class Œ≤] [has_continuous_add Œ≤] : has_add (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª f g, ‚ü®f + g, by simpa only [add_zero] using ((zero_at_infty f).add (zero_at_infty g) :
  tendsto (Œª x : Œ±, f x + g x) (cocompact Œ±) (ùìù (0 + 0)))‚ü©‚ü©

@[simp] lemma coe_add [add_zero_class Œ≤] [has_continuous_add Œ≤] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤) :
  ‚áë(f + g) = f + g := rfl
lemma add_apply [add_zero_class Œ≤] [has_continuous_add Œ≤] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤) :
  (f + g) x = f x + g x := rfl

instance [add_zero_class Œ≤] [has_continuous_add Œ≤] : add_zero_class (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.add_zero_class _ coe_zero coe_add

section add_monoid

variables [add_monoid Œ≤] [has_continuous_add Œ≤] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤)

@[simp] lemma coe_nsmul_rec : ‚àÄ n, ‚áë(nsmul_rec n f) = n ‚Ä¢ f
| 0 := by rw [nsmul_rec, zero_smul, coe_zero]
| (n + 1) := by rw [nsmul_rec, succ_nsmul, coe_add, coe_nsmul_rec]

instance has_nat_scalar : has_scalar ‚Ñï (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª n f, ‚ü®n ‚Ä¢ f, by simpa [coe_nsmul_rec] using (nsmul_rec n f).zero_at_infty'‚ü©‚ü©

@[simp] lemma coe_nsmul (r : ‚Ñï) (f : Œ± ‚ÜíC‚ÇÄ Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ f := rfl
@[simp] lemma nsmul_apply (r : ‚Ñï) (f : Œ± ‚ÜíC‚ÇÄ Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl

instance : add_monoid (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.add_monoid _ coe_zero coe_add (Œª _ _, coe_nsmul _ _)

end add_monoid

instance [add_comm_monoid Œ≤] [has_continuous_add Œ≤] : add_comm_monoid (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.add_comm_monoid _ coe_zero coe_add (Œª _ _, coe_nsmul _ _)

section add_group

variables [add_group Œ≤] [topological_add_group Œ≤] (f g : Œ± ‚ÜíC‚ÇÄ Œ≤)

instance : has_neg (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª f, ‚ü®-f, by simpa only [neg_zero] using (zero_at_infty f : tendsto f (cocompact Œ±) (ùìù 0)).neg‚ü©‚ü©

@[simp] lemma coe_neg : ‚áë(-f) = -f := rfl
lemma neg_apply : (-f) x = -f x := rfl

instance : has_sub (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª f g, ‚ü®f - g,
begin
  rw sub_eq_add_neg,
  simpa only [add_zero] using ((zero_at_infty f).add (zero_at_infty (-g)) :
    tendsto (Œª x, f x + (-g) x) (cocompact Œ±) (ùìù (0 + 0))),
end‚ü©‚ü©

@[simp] lemma coe_sub : ‚áë(f - g) = f - g := rfl
lemma sub_apply : (f - g) x = f x - g x := rfl

@[simp] lemma coe_zsmul_rec : ‚àÄ z, ‚áë(zsmul_rec z f) = z ‚Ä¢ f
| (int.of_nat n) := by rw [zsmul_rec, int.of_nat_eq_coe, coe_nsmul_rec, coe_nat_zsmul]
| -[1+ n] := by rw [zsmul_rec, zsmul_neg_succ_of_nat, coe_neg, coe_nsmul_rec]

instance has_int_scalar : has_scalar ‚Ñ§ (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª n f, ‚ü®n ‚Ä¢ f, by simpa using (zsmul_rec n f).zero_at_infty'‚ü©‚ü©

@[simp] lemma coe_zsmul (r : ‚Ñ§) (f : Œ± ‚ÜíC‚ÇÄ Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ f := rfl
@[simp] lemma zsmul_apply (r : ‚Ñ§) (f : Œ± ‚ÜíC‚ÇÄ Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl

instance [add_group Œ≤] [topological_add_group Œ≤] : add_group (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.add_group _ coe_zero coe_add coe_neg coe_sub (Œª _ _, coe_nsmul _ _)
  (Œª _ _, coe_zsmul _ _)

end add_group

instance [add_comm_group Œ≤] [topological_add_group Œ≤] : add_comm_group (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.add_comm_group _ coe_zero coe_add coe_neg coe_sub (Œª _ _, coe_nsmul _ _)
  (Œª _ _, coe_zsmul _ _)

instance [has_zero Œ≤] {R : Type*} [has_zero R] [smul_with_zero R Œ≤]
  [has_continuous_const_smul R Œ≤] : has_scalar R (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª r f, ‚ü®r ‚Ä¢ f, by simpa [smul_zero] using
  (zero_at_infty f : tendsto f (cocompact Œ±) (ùìù 0)).const_smul r‚ü©‚ü©

@[simp] lemma coe_smul [has_zero Œ≤] {R : Type*} [has_zero R] [smul_with_zero R Œ≤]
  [has_continuous_const_smul R Œ≤] (r : R) (f : Œ± ‚ÜíC‚ÇÄ Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ f := rfl

lemma smul_apply [has_zero Œ≤] {R : Type*} [has_zero R] [smul_with_zero R Œ≤]
  [has_continuous_const_smul R Œ≤] (r : R) (f : Œ± ‚ÜíC‚ÇÄ Œ≤) (x : Œ±) : (r ‚Ä¢ f) x = r ‚Ä¢ f x := rfl

instance [has_zero Œ≤] {R : Type*} [has_zero R] [smul_with_zero R Œ≤]
  [has_continuous_const_smul R Œ≤] : smul_with_zero R (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
function.injective.smul_with_zero ‚ü®_, coe_zero‚ü© fun_like.coe_injective coe_smul

instance [has_zero Œ≤] {R : Type*} [monoid_with_zero R] [mul_action_with_zero R Œ≤]
  [has_continuous_const_smul R Œ≤] : mul_action_with_zero R (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
function.injective.mul_action_with_zero ‚ü®_, coe_zero‚ü© fun_like.coe_injective coe_smul

instance [add_comm_monoid Œ≤] [has_continuous_add Œ≤] {R : Type*} [comm_semiring R] [module R Œ≤]
  [has_continuous_const_smul R Œ≤] : module R (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
function.injective.module R ‚ü®_, coe_zero, coe_add‚ü© fun_like.coe_injective coe_smul

-- this needs to be switched to `topological_semiring Œ≤`
instance [non_unital_semiring Œ≤] [has_continuous_add Œ≤] [has_continuous_mul Œ≤] :
  non_unital_semiring (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
fun_like.coe_injective.non_unital_semiring _ coe_zero coe_add coe_mul (Œª _ _, coe_nsmul _ _)

-- This has to wait for the `topological_ring` refactor.
instance [non_unital_ring Œ≤] : -- [topological_ring Œ≤] :
  non_unital_ring (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
sorry

end algebraic_structure

section metric

open metric set

variables [metric_space Œ≤] [has_zero Œ≤] [zero_at_infty_continuous_map_class F Œ± Œ≤]

protected lemma bounded (f : F) : ‚àÉ C, ‚àÄ x y : Œ±, dist ((f : Œ± ‚Üí Œ≤) x) (f y) ‚â§ C :=
begin
  obtain ‚ü®K : set Œ±, hK‚ÇÅ, hK‚ÇÇ‚ü© := mem_cocompact.mp (tendsto_def.mp (zero_at_infty (f : F)) _
    (closed_ball_mem_nhds (0 : Œ≤) zero_lt_one)),
  obtain ‚ü®C, hC‚ü© := (hK‚ÇÅ.image (map_continuous f)).bounded.subset_ball (0 : Œ≤),
  refine ‚ü®max C 1 + max C 1, (Œª x y, _)‚ü©,
  have : ‚àÄ x, f x ‚àà closed_ball (0 : Œ≤) (max C 1),
  { intro x,
    by_cases hx : x ‚àà K,
    { exact (mem_closed_ball.mp $ hC ‚ü®x, hx, rfl‚ü©).trans (le_max_left _ _) },
    { exact (mem_closed_ball.mp $ mem_preimage.mp (hK‚ÇÇ hx)).trans (le_max_right _ _) } },
  exact (dist_triangle (f x) 0 (f y)).trans
    (add_le_add (mem_closed_ball.mp $ this x) (mem_closed_ball'.mp $ this y)),
end

lemma bounded_range (f : Œ± ‚ÜíC‚ÇÄ Œ≤) : bounded (range f) :=
bounded_range_iff.2 f.bounded

lemma bounded_image (f : Œ± ‚ÜíC‚ÇÄ Œ≤) (s : set Œ±) : bounded (f '' s) :=
f.bounded_range.mono $ image_subset_range _ _

@[priority 100]
instance : bounded_continuous_map_class F Œ± Œ≤ :=
{ coe := Œª f, f,
  coe_injective' := Œª f g h, fun_like.coe_fn_eq.mp h,
  map_continuous := Œª f, map_continuous f,
  map_bounded := Œª f, zero_at_infty_continuous_map.bounded f }

/-- Construct a bounded continuous function from a continuous function vanshing at infinity. -/
@[simps]
def to_bounded_continuous_function (f : Œ± ‚ÜíC‚ÇÄ Œ≤) : Œ± ‚Üí·µá Œ≤ :=
‚ü®f, map_bounded f‚ü©

lemma _root_.function.injective.to_bounded_continuous_function :
  function.injective (to_bounded_continuous_function : (Œ± ‚ÜíC‚ÇÄ Œ≤) ‚Üí Œ± ‚Üí·µá Œ≤) :=
Œª f g h, by { ext, simpa only using fun_like.congr_fun h x, }

-- how can we get the dist from this injective?

variables {C : ‚Ñù} {f g : Œ± ‚ÜíC‚ÇÄ Œ≤}

/-- The uniform distance between two bounded continuous functions -/
noncomputable instance : has_dist (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
‚ü®Œª f g, Inf {C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C}‚ü©

lemma dist_eq : dist f g = Inf {C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C} := rfl

lemma dist_set_exists : ‚àÉ C, 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C :=
begin
  rcases f.bounded_range.union g.bounded_range with ‚ü®C, hC‚ü©,
  refine ‚ü®max 0 C, le_max_left _ _, Œª x, (hC _ _ _ _).trans (le_max_right _ _)‚ü©;
    [left, right]; apply mem_range_self
end

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
lemma dist_coe_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
le_cInf dist_set_exists $ Œª b hb, hb.2 x

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private lemma dist_nonneg' : 0 ‚â§ dist f g :=
le_cInf dist_set_exists (Œª C, and.left)

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h, Œª H, cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®C0, H‚ü©‚ü©

lemma dist_le_iff_of_nonempty [nonempty Œ±] :
  dist f g ‚â§ C ‚Üî ‚àÄ x, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h,
 Œª w, (dist_le (le_trans dist_nonneg (w (nonempty.some ‚Äπ_‚Ä∫)))).mpr w‚ü©

lemma dist_lt_of_nonempty_compact [nonempty Œ±] [compact_space Œ±]
  (w : ‚àÄ x : Œ±, dist (f x) (g x) < C) : dist f g < C :=
begin
  have c : continuous (Œª x, dist (f x) (g x)), { continuity, },
  obtain ‚ü®x, -, le‚ü© :=
    is_compact.exists_forall_ge compact_univ set.univ_nonempty (continuous.continuous_on c),
  exact lt_of_le_of_lt (dist_le_iff_of_nonempty.mpr (Œª y, le y trivial)) (w x),
end

lemma dist_lt_iff_of_compact [compact_space Œ±] (C0 : (0 : ‚Ñù) < C) :
  dist f g < C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) < C :=
begin
  fsplit,
  { intros w x,
    exact lt_of_le_of_lt (dist_coe_le_dist x) w, },
  { by_cases h : nonempty Œ±,
    { resetI,
      exact dist_lt_of_nonempty_compact, },
    { rintro -,
      convert C0,
      apply le_antisymm _ dist_nonneg',
      rw [dist_eq],
      exact cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®le_rfl, Œª x, false.elim (h (nonempty.intro x))‚ü©, }, },
end

lemma dist_lt_iff_of_nonempty_compact [nonempty Œ±] [compact_space Œ±] :
  dist f g < C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) < C :=
‚ü®Œª w x, lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_nonempty_compact‚ü©

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
noncomputable instance : metric_space (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
{ dist_self := Œª f, le_antisymm ((dist_le le_rfl).2 $ Œª x, by simp) dist_nonneg',
  eq_of_dist_eq_zero := Œª f g hfg, by ext x; exact
    eq_of_dist_eq_zero (le_antisymm (hfg ‚ñ∏ dist_coe_le_dist _) dist_nonneg),
  dist_comm := Œª f g, by simp [dist_eq, dist_comm],
  dist_triangle := Œª f g h,
    (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2 $ Œª x,
      le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _)) }

/-- On an empty space, bounded continuous functions are at distance 0 -/
lemma dist_zero_of_empty [is_empty Œ±] : dist f g = 0 :=
dist_eq_zero.2 (eq_of_empty f g)

lemma dist_eq_supr : dist f g = ‚®Ü x : Œ±, dist (f x) (g x) :=
begin
  casesI is_empty_or_nonempty Œ±, { rw [supr_of_empty', real.Sup_empty, dist_zero_of_empty] },
  refine (dist_le_iff_of_nonempty.mpr $ le_csupr _).antisymm (csupr_le dist_coe_le_dist),
  exact dist_set_exists.imp (Œª C hC, forall_range_iff.2 hC.2)
end

lemma lipschitz_evalx (x : Œ±) : lipschitz_with 1 (Œª f : Œ± ‚ÜíC‚ÇÄ Œ≤, f x) :=
lipschitz_with.mk_one $ Œª f g, dist_coe_le_dist x

theorem uniform_continuous_coe : @uniform_continuous (Œ± ‚ÜíC‚ÇÄ Œ≤) (Œ± ‚Üí Œ≤) _ _ coe_fn :=
uniform_continuous_pi.2 $ Œª x, (lipschitz_evalx x).uniform_continuous

lemma continuous_coe : continuous (Œª (f : Œ± ‚ÜíC‚ÇÄ Œ≤) x, f x) :=
uniform_continuous.continuous uniform_continuous_coe

/-- When `x` is fixed, `(f : Œ± ‚ÜíC‚ÇÄ Œ≤) ‚Ü¶ f x` is continuous -/
@[continuity] theorem continuous_evalx {x : Œ±} : continuous (Œª f : Œ± ‚ÜíC‚ÇÄ Œ≤, f x) :=
(continuous_apply x).comp continuous_coe

/-- The evaluation map is continuous, as a joint function of `u` and `x` -/
@[continuity] theorem continuous_eval : continuous (Œª p : (Œ± ‚ÜíC‚ÇÄ Œ≤) √ó Œ±, p.1 p.2) :=
continuous_prod_of_continuous_lipschitz _ 1 (Œª f, map_continuous f) $ lipschitz_evalx

/-- Continuous functions vanishing at infinity taking values in a complete space form a
complete space. -/
instance [complete_space Œ≤] : complete_space (Œ± ‚ÜíC‚ÇÄ Œ≤) :=
complete_of_cauchy_seq_tendsto $ Œª (f : ‚Ñï ‚Üí (Œ± ‚ÜíC‚ÇÄ Œ≤)) (hf : cauchy_seq f),
begin
  /- We have to show that `f n` converges to a continuous function vanishing at infinity
  For this, we prove pointwise convergence to define the limit, then check it is a
  continuous function vanishing at infinity, and then check the metric convergence. -/
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©,
  have f_bdd := Œª x n m N hn hm, le_trans (dist_coe_le_dist x) (b_bound n m N hn hm),
  have fx_cau : ‚àÄ x, cauchy_seq (Œªn, f n x) :=
    Œª x, cauchy_seq_iff_le_tendsto_0.2 ‚ü®b, b0, f_bdd x, b_lim‚ü©,
  choose F hF using Œª x, cauchy_seq_tendsto_of_complete (fx_cau x),
  /- F : Œ± ‚Üí Œ≤,  hF : ‚àÄ (x : Œ±), tendsto (Œª (n : ‚Ñï), f n x) at_top (ùìù (F x))
  `F` is the desired limit function. Check that it is uniformly approximated by `f N` -/
  have fF_bdd : ‚àÄ x N, dist (f N x) (F x) ‚â§ b N :=
    Œª x N, le_of_tendsto (tendsto_const_nhds.dist (hF x))
      (filter.eventually_at_top.2 ‚ü®N, Œª n hn, f_bdd x N n N (le_refl N) hn‚ü©),
  refine ‚ü®‚ü®‚ü®F, _‚ü©, _‚ü©, _‚ü©,
  { /- Check that `F` is continuous, as a uniform limit of continuous functions -/
    have : tendsto_uniformly (Œª n x, f n x) F at_top,
    { refine metric.tendsto_uniformly_iff.2 (Œª Œµ Œµ0, _),
      refine ((tendsto_order.1 b_lim).2 Œµ Œµ0).mono (Œª n hn x, _),
      rw dist_comm,
      exact lt_of_le_of_lt (fF_bdd x n) hn },
    exact this.continuous (eventually_of_forall $ Œª N, map_continuous (f N)) },
  { /- Check that `F` vanishes at infinity. -/
    refine metric.tendsto_nhds.mpr (Œª Œµ hŒµ, eventually_iff.mpr $ mem_cocompact.mpr _),
    rcases metric.tendsto_at_top.1 b_lim (Œµ / 2) (half_pos hŒµ) with ‚ü®N, hN‚ü©,
    obtain ‚ü®t : set Œ±, ht, htŒµ‚ü© := (mem_cocompact.mp $ eventually_iff.1 $
      metric.tendsto_nhds.mp (f N).zero_at_infty' (Œµ / 2) (half_pos hŒµ)),
    refine ‚ü®t, ht, Œª x hx, _‚ü©,
    calc dist (F x) 0 ‚â§ dist (f N x) (F x) + dist (f N x) 0 : dist_triangle_left _ _ _
    ...               < |b N| + Œµ / 2
                      : add_lt_add_of_le_of_lt ((fF_bdd x N).trans (le_abs_self (b N))) (htŒµ hx)
    ...               < Œµ / 2 + Œµ / 2
                      : add_lt_add_right (real.dist_0_eq_abs (b N) ‚ñ∏ (hN N (le_refl N))) _
    ...               = Œµ : add_halves Œµ },
  { /- Check that `F` is close to `f N` in distance terms -/
    refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (Œª _, dist_nonneg) _ b_lim),
    exact Œª N, (dist_le (b0 _)).2 (Œªx, fF_bdd x N) }
end

end metric

end zero_at_infty_continuous_map
